use rug::{Integer as Int, Float};

use std::str::FromStr;

use crate::ast;

grammar;

/// https://doc.rust-lang.org/reference/identifiers.html
/// raw identifiers are not supported (for now)

match {
    "if" => If,
    "else" => Else,
    "as" => As,
    "match" => Match,
    "let" => Let,
    "return" => Return,
    "(break|const|continue|crate|enum|extern|false|fn|for|impl|in|loop|mod|move|mut|pub|ref|self|Self|static|struct|super|trait|true|type|unsafe|use|where|while|async|await|dyn|abstract|become|box|do|final|macro|override|priv|typeof|unsized|virtual|yield|try|union)" => Keyword,
} else {
    r"(?:[a-zA-Z][a-zA-Z0-9_]*|_[a-z A-Z 0-9 _]+)" => Identifier,
    _
}

Ident: ast::Ident = {
    Identifier => ast::Ident {
        inner: <>.to_string()
    },
};

PathIdentSegment: ast::Ident = {
    Ident => <>,
    // r"(super|self|Self|crate|$crate)" => ast::Ident { inner: <>.to_string() },
};

Comma<T>: ast::Comma<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => ast::Comma(v),
        Some(e) => {
            let mut v = v;
            v.push(e);
            ast::Comma(v)
        }
    }
};

Pipe<T>: Vec<T> = {
    <v:(<T> "|")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Path: ast::Path = {
    <leading_colon:"::"?> <path:(<Ident> "::")*> <final_path:Ident> => {
        let mut v = path;
        v.push(final_path);
        ast::Path {
            leading_colon: leading_colon.map(|_| ()),
            segments: v,
        }
    }
};

// Field<T>: (ast::Ident, ast::Expr) = {
//     <v:(<T> ":")*> <e:T?> => match e {
//         None => v,
//         Some(e) => {
//             let mut v = v;
//             v.push(e);
//             v
//         }
//     }
// };


pub Type: ast::Type = {
    "[" <ty:Type> ";" <expr:IntLit> "]" => ast::Type::Array(Box::new(ty), Box::new(expr)),
    Path => ast::Type::Path(<>),
    "fn" "(" <args:Comma<Type>> ")" "->" <ret:Type> => ast::Type::Fn(args, Some(Box::new(ret))),
    "fn" "(" <args:Comma<Type>> ")" => ast::Type::Fn(args, None),
};

Int: Int = {
    // i.e. 1_234_567_890
    r"(0|[1-9]\d*(?:_\d+)*)" => Int::parse(<>).unwrap().into(),
};

pub IntLit: ast::Lit = {
    Int => ast::Lit::Int { val: <>, type_hint: None },

    // i.e. 0xC0FFEE_CAFE
    r"0d\d+(?:_\d+)*" => {
        const BASE_CHAR: char = 'd';
        const BASE: i32 = 10;
        ast::Lit::Int {
            val: Int::parse_radix(&<>.split(BASE_CHAR).nth(1).unwrap(), BASE).unwrap().into(),
            type_hint: None,
        }
    },

    // i.e. 0xC0FFEE_CAFE
    r"0x[0-9a-fA-F]+(?:_[0-9a-fA-F]+)*" => {
        const BASE_CHAR: char = 'x';
        const BASE: i32 = 16;
        ast::Lit::Int {
            val: Int::parse_radix(&<>.split(BASE_CHAR).nth(1).unwrap(), BASE).unwrap().into(),
            type_hint: None,
        }
    },

    // i.e. 0o72
    r"0o[0-7]+(?:_[0-7]+)*" => {
        const BASE_CHAR: char = 'o';
        const BASE: i32 = 8;
        ast::Lit::Int {
            val: Int::parse_radix(&<>.split(BASE_CHAR).nth(1).unwrap(), BASE).unwrap().into(),
            type_hint: None,
        }
    },

    // i.e. 0b00110010_00101011_00110010_00111101_00110101
    r"0b[01]+(?:_[01]+)*" => {
        const BASE_CHAR: char = 'b';
        const BASE: i32 = 2;
        ast::Lit::Int {
            val: Int::parse_radix(&<>.split(BASE_CHAR).nth(1).unwrap(), BASE).unwrap().into(),
            type_hint: None,
        }
    },

    // i.e. 36#HI, an Int in base 36
    r"(?:2|3|4|5|6|7|8|9|10|11|12|13|14|15|16|17|18|19|20|21|22|23|24|25|26|27|28|29|30|31|32|33|34|35|36)#[a-zA-Z0-9]+(?:_[a-zA-Z0-9]+)*" => {
        const BASE_CHAR: char = '#';
        let base = <>.split(BASE_CHAR).next().map(i32::from_str).unwrap().unwrap();
        ast::Lit::Int {
            val: Int::parse_radix(&<>.split(BASE_CHAR).nth(1).unwrap(), base).unwrap().into(),
            type_hint: None,
        }
    },
};

pub Float: ast::Lit = r"(?:\d+\.\d*|\d*\.\d+|\d+)(?:[eE][-+]?\d+)?f(?:16|32|64|80|128|256)" => {
    let format = <>.split('f').nth(1).map(usize::from_str).unwrap().unwrap();
    let precision = match format {
        16 => 11,
        32 => 24,
        64 => 53,
        80 => 64,
        128 => 113,
        256 => 237,
        _ => unreachable!()
    };
    let float = Float::parse(<>.split('f').next().unwrap()).unwrap();
    ast::Lit::Float {
        val: Float::with_val(precision, float)
    }
};

LiteralExpression: ast::Expr = {
    IntLit => ast::Expr::Lit(<>),
    Float => ast::Expr::Lit(<>),
};

PathExpression: ast::Expr = Path => ast::Expr::Path(<>);


Statement: ast::Stmt = {
    // Item,
    Let <pat:Pat> <ty:(":" <Type>)?> <expr:("=" <Expr>)?> ";" => ast::Stmt::Local(pat, ty, expr),
    <expr:ExprWithoutBlockWithReturn> ";" => ast::Stmt::Expr(expr, true),
    <expr:ExprWithBlock> <semi:";"?> => ast::Stmt::Expr(expr, semi.is_some()),
};

Statements: Vec<ast::Stmt> = {
    <Statement+> => <>,
    <mut v:Statement+> <e:ExprWithoutBlockWithReturn> => {
        v.push(ast::Stmt::Expr(e, false));
        v
    },
    ExprWithoutBlockWithReturn => vec![ast::Stmt::Expr(<>, false)],
};

BlockExpression: ast::Expr = {
    "{" <mut v:Statements?> "}" => {
        let v = v.unwrap_or_default();
        ast::Expr::Block(ast::Block(v))
    },
};


Unary: ast::Expr = {
    "!" <ExprWithoutBlockOrOperator> => ast::Expr::Unary(ast::UnOp::Not, Box::new(<>)),
    "-" <ExprWithoutBlockOrOperator> => ast::Expr::Unary(ast::UnOp::Neg, Box::new(<>)),
    "!" <ExprWithBlock> => ast::Expr::Unary(ast::UnOp::Not, Box::new(<>)),
    "-" <ExprWithBlock> => ast::Expr::Unary(ast::UnOp::Neg, Box::new(<>)),
    ExprWithoutBlockOrOperator => <>,
    // ExprWithBlock => <>,
};

Cast: ast::Expr = {
    <expr:Unary> As <ty:Type> => ast::Expr::Cast(Box::new(expr), Box::new(ty)),
    Unary => <>,
};

Arith0: ast::Expr = {
    <l:Arith0> "**" <r:Cast> => ast::Expr::Binary(Box::new(l), ast::BinOp::Exp, Box::new(r)),
    Cast => <>,
};

Arith1: ast::Expr = {
    <l:Arith1> "*" <r:Arith0> => ast::Expr::Binary(Box::new(l), ast::BinOp::Mul, Box::new(r)),
    <l:Arith1> "/" <r:Arith0> => ast::Expr::Binary(Box::new(l), ast::BinOp::Div, Box::new(r)),
    <l:Arith1> "%" <r:Arith0> => ast::Expr::Binary(Box::new(l), ast::BinOp::Rem, Box::new(r)),
    Arith0 => <>,
};

Arith2: ast::Expr = {
    <l:Arith2> "+" <r:Arith1> => ast::Expr::Binary(Box::new(l), ast::BinOp::Add, Box::new(r)),
    <l:Arith2> "-" <r:Arith1> => ast::Expr::Binary(Box::new(l), ast::BinOp::Sub, Box::new(r)),
    Arith1 => <>,
};

Bin0: ast::Expr = {
    <l:Bin0> "<<" <r:Arith2> => ast::Expr::Binary(Box::new(l), ast::BinOp::Shl, Box::new(r)),
    <l:Bin0> ">>" <r:Arith2> => ast::Expr::Binary(Box::new(l), ast::BinOp::Shr, Box::new(r)),
    Arith2 => <>,
};

Bin1: ast::Expr = {
    <l:Bin1> "&" <r:Bin0> => ast::Expr::Binary(Box::new(l), ast::BinOp::BitAnd, Box::new(r)),
    Bin0 => <>,
};

Bin2: ast::Expr = {
    <l:Bin2> "^" <r:Bin1> => ast::Expr::Binary(Box::new(l), ast::BinOp::BitXor, Box::new(r)),
    Bin1 => <>,
};

Bin3: ast::Expr = {
    <l:Bin3> "|" <r:Bin2> => ast::Expr::Binary(Box::new(l), ast::BinOp::BitOr, Box::new(r)),
    Bin2 => <>,
};

ComparisonExpression: ast::Expr = {
    <l:Bin3> "<" <r:Bin3> => ast::Expr::Binary(Box::new(l), ast::BinOp::Lt, Box::new(r)),
    <l:Bin3> "<=" <r:Bin3> => ast::Expr::Binary(Box::new(l), ast::BinOp::Le, Box::new(r)),
    <l:Bin3> ">" <r:Bin3> => ast::Expr::Binary(Box::new(l), ast::BinOp::Gt, Box::new(r)),
    <l:Bin3> ">=" <r:Bin3> => ast::Expr::Binary(Box::new(l), ast::BinOp::Ge, Box::new(r)),
    <l:Bin3> "==" <r:Bin3> => ast::Expr::Binary(Box::new(l), ast::BinOp::Eq, Box::new(r)),
    Bin3 => <>,
};

AssignExpression: ast::Expr = {
    <l:ComparisonExpression> "=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::Eq, Box::new(r)),
    <l:ComparisonExpression> "+=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::AddEq, Box::new(r)),
    <l:ComparisonExpression> "-=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::SubEq, Box::new(r)),
    <l:ComparisonExpression> "*=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::MulEq, Box::new(r)),
    <l:ComparisonExpression> "/=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::DivEq, Box::new(r)),
    <l:ComparisonExpression> "%=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::RemEq, Box::new(r)),
    <l:ComparisonExpression> "&=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::BitAndEq, Box::new(r)),
    <l:ComparisonExpression> "|=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::BitOrEq, Box::new(r)),
    <l:ComparisonExpression> "^=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::BitXorEq, Box::new(r)),
    <l:ComparisonExpression> "<<=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::ShlEq, Box::new(r)),
    <l:ComparisonExpression> ">>=" <r:ComparisonExpression> => ast::Expr::Assign(Box::new(l), ast::AssOp::ShrEq, Box::new(r)),
    Comparison => <>,
};

OperatorExpression: ast::Expr = {
    AssignExpression => <>,
};

GroupedExpression: ast::Expr = {
    "(" <Expr> ")" => <>,
};

ArrayExpression: ast::Expr = {
    "[" <val:Expr> ";" <repeat:Expr> "]" => ast::Expr::Repeat(Box::new(val), Box::new(repeat)),
    "[" <Comma<Expr>> "]" => ast::Expr::Array(<>),
};

IndexExpression: ast::Expr = <on:ExprWithoutBlockOrOperator> "[" <index:Expr> "]" => ast::Expr::Index(Box::new(on), Box::new(index));

TupleElements: Vec<ast::Expr> = <mut v:(<Expr> ",")+> <e:Expr?> => {
    if let Some(e) = e {
        v.push(e);
    }
    v
};
TupleExpression: ast::Expr = "(" <TupleElements?> ")" => ast::Expr::Tuple(ast::Comma(<>.unwrap_or_default()));


TupleIndexingExpression: ast::Expr = {
    <base:ExprWithBlock> "." <unnamed:Int> => ast::Expr::Field(Box::new(base), ast::Member::Unnamed(unnamed)),
    <base:ExprWithoutBlockOrOperator> "." <unnamed:Int> => ast::Expr::Field(Box::new(base), ast::Member::Unnamed(unnamed)),
};

// TODO: struct, enum expressions


CallExpression: ast::Expr = {
    <func:ExprWithoutFieldOrBlock> "(" <args:Comma<Expr>> ")" => ast::Expr::Call(Box::new(func), args),
};

MethodCallExpression: ast::Expr = {
    <receiver:ExprWithBlock> "." <method:PathIdentSegment> "(" <args:Comma<Expr>> ")" => ast::Expr::MethodCall(Box::new(receiver), method, args),
    <receiver:ExprWithoutBlockOrOperator> "." <method:PathIdentSegment> "(" <args:Comma<Expr>> ")" => ast::Expr::MethodCall(Box::new(receiver), method, args),
};

FieldExpression: ast::Expr = {
    <base:ExprWithBlock> "." <named:Ident> => ast::Expr::Field(Box::new(base), ast::Member::Named(named)),
    <base:ExprWithoutBlockOrOperator> "." <named:Ident> => ast::Expr::Field(Box::new(base), ast::Member::Named(named)),
};

// todo: closure expression

RangeExpression: ast::Expr = {
    <l:LiteralExpression?> ".." <r:LiteralExpression?> => ast::Expr::Range(l.map(|l| Box::new(l)), r.map(|r| Box::new(r)), false),
    <l:LiteralExpression?> "..=" <r:LiteralExpression> => ast::Expr::Range(l.map(|l| Box::new(l)), Some(Box::new(r)), true),
};

CondIf: ast::Expr = If <Expr> => <>;

// todo: if let expression
pub IfExpression: ast::Expr = {
    <c:CondIf> <b:BlockExpression> <els:(Else <IfExpression>)> => {
        ast::Expr::If(Box::new(c), Box::new(b), Some(Box::new(els)))
    },
    <c:CondIf> <b:BlockExpression> <els:(Else <BlockExpression>)?> => {
        ast::Expr::If(Box::new(c), Box::new(b), els.map(Box::new))
    },
};

MatchArm: ast::Arm = {
    <pat:Pat> <guard:CondIf?> "=>" <body:Expr> => ast::Arm {
        pat,
        guard,
        body,
    },
};

MatchExpression: ast::Expr = {
    Match <e:Expr> "{" <arms:Comma<MatchArm>> "}" => ast::Expr::Match(Box::new(e), arms)
};

ReturnExpression: ast::Expr = {
    Return <Expr?> => ast::Expr::Return(<>.map(Box::new)),
};

ExprWithoutFieldOrBlock: ast::Expr = {
    LiteralExpression => <>,
    PathExpression => <>,
    GroupedExpression => <>,
    ArrayExpression => <>,
    IndexExpression => <>,
    TupleIndexingExpression => <>,
    CallExpression => <>,
    MethodCallExpression => <>,
    RangeExpression => <>,
};

ExprWithoutBlockOrOperator: ast::Expr = {
    ExprWithoutFieldOrBlock => <>,
    FieldExpression => <>,
};

ExprWithoutBlockWithReturn: ast::Expr = {
    OperatorExpression => <>,
    ReturnExpression => <>,
};

ExprWithBlock: ast::Expr = {
    BlockExpression => <>,
    IfExpression => <>,
    MatchExpression => <>,
};

pub Expr: ast::Expr = {
    OperatorExpression => <>,
    ExprWithBlock,
};

pub Pat: ast::Pat = {
    Ident => ast::Pat::Ident(<>),
    IntLit => ast::Pat::Lit(<>)
}
